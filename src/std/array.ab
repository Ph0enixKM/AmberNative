/// Returns index of the first value found in the specified array.
///
/// If the value is not found, the function returns -1.
pub fun array_first_index(array, value): Num {
    for index, element in array {
        if value as Text == element as Text {
            return index
        }
    }
    return -1
}

/// Searches for a value in an array and returns an array with the index of the various items.
pub fun array_search(array, value): [Num] {
    let result = [Num]
    for index, element in array {
        if value as Text == element as Text {
            result += [index]
        }
    }
    return result
}

/// Checks if a value is in the array.
pub fun includes(array, value) {
    let result = array_first_index(array, value)
    return result >= 0
}

/// Returns the last element in the array
pub fun last(array) {
    return array[len(array) - 1]
}

/// Removes an element at the index from the array
pub fun remove_at(ref array: [], index: Num): Null {
    trust $ unset {nameof array}[{index}] $
}

/// Removes an element at the index from the array, and returns it
pub fun extract_at(ref array, index) {
    let elem = array[index]
    remove_at(array, index)
    return elem
}
